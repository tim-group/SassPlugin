plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

ext {
    buildNumber = System.getenv("ORIGINAL_BUILD_NUMBER") ?: System.getenv("BUILD_NUMBER")
}

group = 'com.timgroup'
if (buildNumber) version = '1.1.' + buildNumber

repositories {
    maven {
        url = "${repoUrl}/groups/public"
    }
}

gradlePlugin {
    plugins {
        sass {
            id = "com.youdevise.sass"
            implementationClass = "com.youdevise.gradle.plugins.SassPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            url "${project.repoUrl}/repositories/yd-release-candidates"
            credentials {
                username = project.repoUsername
                password = project.repoPassword
            }
        }
    }
}

model {
    publishing {
        publications {
            pluginMaven {
                artifact sourcesJar
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

dependencies {
    compile gradleApi()
    compile localGroovy()

    runtime "org.jruby:jruby-complete:1.7.27"
    runtime "me.n4u.sass:sass-gems:3.1.16"

    testCompile "junit:junit:4.12"
    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude module: 'groovy-all'
    }
}
